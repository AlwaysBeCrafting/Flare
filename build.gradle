apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'



repositories {
	mavenCentral()
}

dependencies {
	testCompile group: 'junit', name: 'junit', version: '4.11'
}



// Generate and bundle extra JARs
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}



// Sign all archives
signing {
	sign configurations.archives
}



// Metadata
group 'stream.alwaysbecrafting'
version '1.0.0-alpha2'

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'Flare'
				packaging 'jar'
				artifactId 'flare'
				description 'A tiny ECS for simple games'
				url 'https://github.com/AlwaysBeCrafting/Flare'

				scm {
					connection 'scm:git@github.com:AlwaysBeCrafting/Flare.git'
					developerConnection 'scm:git@github.com:AlwaysBeCrafting/Flare.git'
					url 'git@github.com:AlwaysBeCrafting/Flare.git'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'alexander-prime'
						name 'Alexander Peters'
						email 'rainesabaal@gmail.com'
					}
				}
			}
		}
	}
}